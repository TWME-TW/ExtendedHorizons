plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.19'
    id 'checkstyle'
}

group = 'me.mapacheee'
version = '2.3.0'
description = 'ExtendedHorizons - Advanced view distance extension plugin'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
    name = 'papermc'
    url = 'https://repo.papermc.io/repository/maven-public/'
  }
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'placeholderapi'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = 'codemc-repo'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        name = 'luckperms'
        url = 'https://repo.luckperms.net/'
    }
    maven {
        name = 'winter-framework'
        url = 'https://repo.maven.apache.org/maven2/'
    }
    maven { url = uri("https://repo.codemc.io/repository/maven-releases/") }

    maven { url = uri("https://repo.codemc.io/repository/maven-snapshots/") }
}

dependencies {
    // Winter Framework
    api("com.thewinterframework:paper:1.0.4") {
        exclude group: 'io.papermc.paper', module: 'paper-api'
    }
    annotationProcessor("com.thewinterframework:paper:1.0.4")
    
    api('com.thewinterframework:configuration:1.0.2') {
        exclude group: 'io.papermc.paper', module: 'paper-api'
    }
    annotationProcessor('com.thewinterframework:configuration:1.0.2')
    
    api('com.thewinterframework:command:1.0.1') {
        exclude group: 'io.papermc.paper', module: 'paper-api'
    }
    annotationProcessor('com.thewinterframework:command:1.0.1')
    
    // Paper API
    //compileOnly('io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT')
    paperweight.paperDevBundle("1.21.11-R0.1-SNAPSHOT")

    // PacketEvents
    compileOnly("com.github.retrooper:packetevents-spigot:2.11.1")

    // SQLite
    compileOnly("org.xerial:sqlite-jdbc:3.51.1.0")

    // PlaceholderAPI
    compileOnly("me.clip:placeholderapi:2.11.6")

    // LuckPerms API
    compileOnly("net.luckperms:api:5.4")    
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

shadowJar {
    relocate 'org.h2', 'me.mapacheee.extendedhorizons.libs.h2'
    relocate 'com.thewinterframework', 'me.mapacheee.extendedhorizons.libs.winter'
    relocate 'com.google.inject', 'me.mapacheee.extendedhorizons.libs.guice'
    relocate 'com.google.common', 'me.mapacheee.extendedhorizons.libs.guava'
    relocate 'org.checkerframework', 'me.mapacheee.extendedhorizons.libs.checkerframework'
    relocate 'javax.inject', 'me.mapacheee.extendedhorizons.libs.inject'
    relocate 'aopalliance', 'me.mapacheee.extendedhorizons.libs.aopalliance'

    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/versions/**'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude 'META-INF/native-image/**'
    exclude 'module-info.class'
    exclude '**/module-info.class'
    exclude 'META-INF/*.kotlin_module'
    
    mergeServiceFiles()
    
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Multi-Release': 'false'
        )
    }

    archiveBaseName = 'ExtendedHorizons'
    archiveClassifier = ''
    archiveVersion = project.version

    configurations = [project.configurations.runtimeClasspath]
    
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.build {
    dependsOn shadowJar
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand project.properties
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('checkstyle.xml')
    ignoreFailures = true
}

